SAMPLES = ["AAAA_2007.01.21"]

rule ALL:
    '''
    FIXME: For now a single target file is used to test different steps of the pipeline
    '''
    #input:"../protocols/07_skullstripped/AAAA_2007.01.21/AAAA_2007.01.21_t1_lps_n4_rt1ce_rsri_brain.nii.gz"
    #input:"../protocols/08_normalized/AAAA_2007.01.21/AAAA_2007.01.21_t1_lps_n4_rt1ce_rsri_brain_zscore.nii.gz"    
    #input:"../protocols/09_segmented_deepmedic/AAAA_2007.01.21/AAAA_2007.01.21_lps_n4_rt1ce_rsri_brain_zscore_segmdm.nii.gz"    
    #input:"../protocols/10_segmented_tumor/AAAA_2007.01.21/AAAA_2007.01.21_lps_n4_rt1ce_rsri_brain_zscore_segmdm_tumor.nii.gz"
    #input:"../protocols/11_tumor_n4/AAAA_2007.01.21/AAAA_2007.01.21_lps_n4_rt1ce_rsri_brain_zscore_segmdm_tumor_n4.nii.gz"
    input:"../protocols/12_susan/AAAA_2007.01.21/AAAA_2007.01.21_lps_n4_rt1ce_rsri_brain_zscore_segmdm_tumor_n4_susan.nii.gz"

ruleorder: reorient_dti_img > reorient_img
    
rule reorient_dti_img:
    """
    Reorient a DTI image to LPS orientation
    This is done in a limited way only for LAS images, by modifying the bvec file
    """
    input:
        dti="../data/{sample}/{sample}_dti.nii.gz",
        bval="../data/{sample}/{sample}_dti.bval",
        bvec="../data/{sample}/{sample}_dti.bvec"
    output:
        dti="../protocols/01_reoriented/{sample}/{sample}_dti_lps.nii.gz",
        bval="../protocols/01_reoriented/{sample}/{sample}_dti_lps.bval",
        bvec="../protocols/01_reoriented/{sample}/{sample}_dti_lps.bvec"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/reorient_dti_to_LPS.sh {input.dti} {output.dti} RAI"

rule reorient_img:
    """
    Reorient a 3D image to LPS orientation
    """
    input:
        "../data/{sample}/{sample}_{mod}.nii.gz"
    output:
        "../protocols/01_reoriented/{sample}/{sample}_{mod}_lps.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/reorient_to_LPS.sh {input} {output} RAI"

rule run_n4:
    """
    N4 bias correction
    """
    input:
        "../protocols/01_reoriented/{sample}/{sample}_{mod}_lps.nii.gz"
    output:
        "../protocols/02_biascorr/{sample}/{sample}_{mod}_lps_n4.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/biascorr_n4.sh {input} {output}"
        
rule to_t1ce_calcmat:
    """
    Calculate matrix to align image to t1ce image (within subjects)
    Matrix is calculated independently for different modalities
    """
    input:
        img="../protocols/02_biascorr/{sample}/{sample}_{mod}_lps_n4.nii.gz",
        ref="../protocols/02_biascorr/{sample}/{sample}_t1ce_lps_n4.nii.gz"
    output:
        "../protocols/03_aligned_t1ce/{sample}/{sample}_{mod}_lps_n4_rt1ce.mat"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/register_calcmat.sh {input.img} {input.ref} {output}"

rule to_t1ce_applymat:
    """
    Apply matrix to align image to t1ce image (within subjects)
    """
    input:
        img="../protocols/02_biascorr/{sample}/{sample}_{mod}_lps_n4.nii.gz",
        ref="../protocols/02_biascorr/{sample}/{sample}_t1ce_lps_n4.nii.gz",
        mat="../protocols/03_aligned_t1ce/{sample}/{sample}_{mod}_lps_n4_rt1ce.mat"
    output:
        "../protocols/03_aligned_t1ce/{sample}/{sample}_{mod}_lps_n4_rt1ce.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/register_applymat.sh {input.img} {input.ref} {input.mat} {output}"
        
rule to_sri_calcmat:
    """
    Calculate matrix to align image to sri image (between subjects)
    A single matrix is calculated only using the T1 modality
    """
    input:
        img="../protocols/03_aligned_t1ce/{sample}/{sample}_t1_lps_n4_rt1ce.nii.gz",
        ref="../templates/spgr_unstrip-RAI.nii.gz"
    output:
        "../protocols/04_aligned_sri/{sample}/{sample}_t1_lps_n4_rt1ce_rsri.mat"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/register_calcmat.sh {input.img} {input.ref} {output}"

rule to_sri_applymat:
    """
    Apply matrix to align images to SRI image (between subjects)
    All modalities use the same matrix from T1 registration to SRI
    """
    input:
        img="../protocols/03_aligned_t1ce/{sample}/{sample}_{mod}_lps_n4_rt1ce.nii.gz",
        ref="../templates/spgr_unstrip-RAI.nii.gz",
        mat="../protocols/04_aligned_sri/{sample}/{sample}_t1_lps_n4_rt1ce_rsri.mat"
    output:
        "../protocols/04_aligned_sri/{sample}/{sample}_{mod}_lps_n4_rt1ce_rsri.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/register_applymat.sh {input.img} {input.ref} {input.mat} {output}"

rule run_dm_mask:
    """
    Calculate deep_medic mask
    """
    input:
        t1="../protocols/04_aligned_sri/{sample}/{sample}_t1_lps_n4_rt1ce_rsri.nii.gz",
        t1ce="../protocols/04_aligned_sri/{sample}/{sample}_t1ce_lps_n4_rt1ce_rsri.nii.gz",
        t2="../protocols/04_aligned_sri/{sample}/{sample}_t2_lps_n4_rt1ce_rsri.nii.gz",
        fl="../protocols/04_aligned_sri/{sample}/{sample}_flair_lps_n4_rt1ce_rsri.nii.gz"
    output:
        "../protocols/05_brainmask_deepmedic/{sample}/{sample}_t1_lps_n4_rt1ce_rsri_maskDM.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/maskbrain_deepmedic.sh {input.t1} {input.t1ce} {input.t2} {input.fl} {output}"
 
rule run_ma_mask: 
    """
    Calculate brain_mage mask
    FIXME: this step failed for me due to dependencies, so it's not used in later mask selection step for now
    """
    input:
        "../protocols/04_aligned_sri/{sample}/{sample}_{mod}_lps_n4_rt1ce_rsri.nii.gz"
    output:
        "../protocols/06_brainmask_brainmage/{sample}/{sample}_{mod}_lps_n4_rt1ce_rsri_maskMA.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/maskbrain_brainmage.sh {input} {output}"
 
rule select_mask:
    """
    Rule to select between two brain masks
    FIXME: THIS STEP IS NORMALLY DONE MANUALLY (for now the script just selects the first input as the output mask)
    """
    input:
        m1="../protocols/05_brainmask_deepmedic/{sample}/{sample}_t1_lps_n4_rt1ce_rsri_maskDM.nii.gz",
        #m2="../protocols/06_brainmask_brainmage/{sample}/{sample}_t1_lps_n4_rt1ce_rsri_maskMA.nii.gz",
        #m3="../protocols/06_brainmask_brainmage/{sample}/{sample}_t1ce_lps_n4_rt1ce_rsri_maskMA.nii.gz"        
    output:
        "../protocols/07_skullstripped/{sample}/{sample}_brainmask.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        #"bash ./utils/sel_brainmask.sh {input.m1} {input.m2} {input.m3} {output}"
        "bash ./utils/select_brainmask.sh {input.m1} {output}"
 
rule apply_mask:
    """
    Apply brain mask to images
    """
    input:
        img="../protocols/04_aligned_sri/{sample}/{sample}_{mod}_lps_n4_rt1ce_rsri.nii.gz",
        mask="../protocols/07_skullstripped/{sample}/{sample}_brainmask.nii.gz"
    output:
        "../protocols/07_skullstripped/{sample}/{sample}_{mod}_lps_n4_rt1ce_rsri_brain.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/apply_brainmask.sh {input.img} {input.mask} {output}"

rule zscore_img: 
    """
    Normalize images usinf z-score transform
    """
    input:
        "../protocols/07_skullstripped/{sample}/{sample}_{mod}_lps_n4_rt1ce_rsri_brain.nii.gz"
    output:
        "../protocols/08_normalized/{sample}/{sample}_{mod}_lps_n4_rt1ce_rsri_brain_zscore.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/normalize_zscore.sh {input} {output}"
 
rule dm_seg:
    """
    Apply deep_medic segmentation
    FIXME: this step failed for me due to dependencies
    """
    input:
        t1="../protocols/08_normalized/{sample}/{sample}_t1_lps_n4_rt1ce_rsri_brain_zscore.nii.gz",
        t1ce="../protocols/08_normalized/{sample}/{sample}_t1ce_lps_n4_rt1ce_rsri_brain_zscore.nii.gz",
        t2="../protocols/08_normalized/{sample}/{sample}_t2_lps_n4_rt1ce_rsri_brain_zscore.nii.gz",
        fl="../protocols/08_normalized/{sample}/{sample}_flair_lps_n4_rt1ce_rsri_brain_zscore.nii.gz",
    output:
        "../protocols/09_segmented_deepmedic/{sample}/{sample}_lps_n4_rt1ce_rsri_brain_zscore_segmdm.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/segment_deepmedic.sh {input} {output}"
        
rule mask_tumor:
    """
    Mask tumor
    FIXME: this is a placeholder for now, not implemented yet
    """
    input:
        "../protocols/09_segmented_deepmedic/{sample}/{sample}_lps_n4_rt1ce_rsri_brain_zscore_segmdm.nii.gz"
    output:
        "../protocols/10_segmented_tumor/{sample}/{sample}_lps_n4_rt1ce_rsri_brain_zscore_segmdm_tumor.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/mask_tumor.sh {input} {output}"

rule n4_tumor:
    """
    Mask tumor
    FIXME: this is a placeholder for now, not implemented yet
    """
    input:
        "../protocols/10_segmented_tumor/{sample}/{sample}_lps_n4_rt1ce_rsri_brain_zscore_segmdm_tumor.nii.gz"
    output:
        "../protocols/11_tumor_n4/{sample}/{sample}_lps_n4_rt1ce_rsri_brain_zscore_segmdm_tumor_n4.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/biascorr_n4.sh {input} {output}"

rule run_susan:
    """
    Mask tumor
    FIXME: this is a placeholder for now, not implemented yet
    """
    input:
        "../protocols/11_tumor_n4/{sample}/{sample}_lps_n4_rt1ce_rsri_brain_zscore_segmdm_tumor_n4.nii.gz"
    output:
        "../protocols/12_susan/{sample}/{sample}_lps_n4_rt1ce_rsri_brain_zscore_segmdm_tumor_n4_susan.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/susan.sh {input} {output}"
