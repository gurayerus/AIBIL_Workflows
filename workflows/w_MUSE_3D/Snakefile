import pandas as pd

df = pd.read_csv('../lists/IDList.csv')
DICT_BLID = dict(zip(df['MRID'], df['BLID']))
LIST_MRID = list(DICT_BLID.keys())
##BLIDS = ['OAS30001_MR_d0129', 'OAS30002_MR_d0371']


rule ALL:
    #input:"../protocols/MUSE_BL/sub-1000173_ses-NAPFU03/sub-1000173_ses-NAPFU03_T1_LPS_N4_brain_muse-ss_Mean_fastbc_muse.nii.gz"
    #input:"../protocols/MUSE_BL/sub-1000173_ses-NAPBL00/sub-1000173_ses-NAPBL00_T1_LPS_N4_brain_muse-#ss_Mean_fastbc_muse.nii.gz"
    input:"../protocols/MUSE_4D/sub-1000173_ses-NAPBL00/sub-1000173_ses-NAPBL00_T1_LPS_N4_brain_muse-ss_Mean_fastbc_muse.nii.gz"


rule reorientLPS:
    """
    Reorient a 3D image to LPS orientation
    """
    input:
        "../input/data/{MRID}/{MRID}_{mod}.nii.gz"
    output:
        "../protocols/ReOrientedLPS/{MRID}/{MRID}_{mod}_LPS.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/run_reorientLPS.sh {input} {output}"

rule biascorr_N4:
    """
    N4 bias correction
    """
    input:
        "../protocols/ReOrientedLPS/{MRID}/{MRID}_{mod}_LPS.nii.gz"
    output:
        "../protocols/BiasCorrected/{MRID}/{MRID}_{mod}_LPS_N4.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/run_biascorr_N4.sh {input} {output}"

rule bmask_MUSE:
    """
    MUSE skull stripping
    - Applied to all images, both BL and FU
    """
    input:
        "../protocols/BiasCorrected/{MRID}/{MRID}_T1_LPS_N4.nii.gz"
    output:
        "../protocols/Skull-Stripped/{MRID}/{MRID}_T1_LPS_N4_brainmask_muse-ss.nii.gz",
    resources:
        mem_mb=8000
    shell:
        "bash ./utils/run_bmask_MUSE.sh {input} {output}"

rule bmask_MUSE_4D:
    """
    Use baseline MUSE bmasks to calculate 4D bmask
    - Applied to all images both BL and FU
    - For BL images an image is warped to itself to calculate the 4D mask; this looks redundant
    """
    input:
        img="../protocols/BiasCorrected/{MRID}/{MRID}_T1_LPS_N4.nii.gz",
        mask="../protocols/Skull-Stripped/{MRID}/{MRID}_T1_LPS_N4_brainmask_muse-ss.nii.gz",
        blimg = lambda wildcards : "../protocols/BiasCorrected/" + DICT_BLID[wildcards.MRID] + "/" + DICT_BLID[wildcards.MRID] + "_T1_LPS_N4.nii.gz",
        blmask = lambda wildcards : "../protocols/Skull-Stripped/" + DICT_BLID[wildcards.MRID] + "/" + DICT_BLID[wildcards.MRID] + "_T1_LPS_N4_brainmask_muse-ss.nii.gz"
    #params:
        #blid=lambda wildcards:DICT_BLID[wildcards.MRID]
    output:
        "../protocols/Skull-Stripped_4D/{MRID}/{MRID}_T1_LPS_N4_brain_muse-ss_Mean.nii.gz"
    resources:
        mem_mb=8000
    shell:
        "bash ./utils/run_bmask_MUSE_4D.sh {input} {output}"

rule bmask_T1T2:
    """
    T2 based skull stripping
    """
    input:
        t1="../protocols/BiasCorrected/{MRID}/{MRID}_T1_LPS_N4.nii.gz",
        t2="../protocols/BiasCorrected/{MRID}/{MRID}_T2_LPS_N4.nii.gz",
        bmask="../protocols/Skull-Stripped_4D/{MRID}/{MRID}_T1_LPS_N4_brain_muse-ss_Mean.nii.gz"
    output:
        "../protocols/Skull-Stripped_T2/{MRID}/{MRID}_T1_LPS_N4_brainmask_muse-ss_Mean_ICV.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/run_bmask_T1T2.sh {input} {output}"

rule biascorr_FAST:
    """
    FAST bias correction
    """
    input:
        "../protocols/Skull-Stripped_4D/{MRID}/{MRID}_T1_LPS_N4_brain_muse-ss_Mean.nii.gz"
    output:
        "../protocols/fastbc/{MRID}/{MRID}_T1_LPS_N4_brain_muse-ss_Mean_fastbc.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/run_biascorr_FAST.sh {input} {output}"
        
rule MUSE_BL:
    """
    Run MUSE; applied only to BL images
    """
    input:
        "../protocols/fastbc/{MRID}/{MRID}_T1_LPS_N4_brain_muse-ss_Mean_fastbc.nii.gz"
    output:
        "../protocols/MUSE_BL/{MRID}/{MRID}_{mod}_LPS_N4_brain_muse-ss_Mean_fastbc_muse.nii.gz"
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/run_MUSE.sh {input} {output}"

rule MUSE_4D:
    """
    Run MUSE using BL warps 
    - Applied to all images both BL and FU
    - For BL images an image is warped to itself to calculate the 4D ROIs; this looks redundant    
    """
    input:
        img="../protocols/fastbc/{MRID}/{MRID}_T1_LPS_N4_brain_muse-ss_Mean_fastbc.nii.gz",
        blimg = lambda wildcards: "../protocols/fastbc/" + DICT_BLID[wildcards.MRID] + "/" + DICT_BLID[wildcards.MRID] + "_T1_LPS_N4_brain_muse-ss_Mean_fastbc.nii.gz",
        blseg = lambda wildcards: "../protocols/MUSE_BL/" + DICT_BLID[wildcards.MRID] + "/" + DICT_BLID[wildcards.MRID] + "_T1_LPS_N4_brain_muse-ss_Mean_fastbc_muse.nii.gz"
    params:
        BLID=lambda wildcards:DICT_BLID[wildcards.MRID]
    output:
        "../protocols/MUSE_4D/{MRID}/{MRID}_T1_LPS_N4_brain_muse-ss_Mean_fastbc_muse.nii.gz"
    resources:
        mem_mb=8000
    shell:
        "bash ./utils/run_MUSE_4D.sh {input} {output} {wildcards.MRID} {params.BLID}"
        
